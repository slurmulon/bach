(* Core *)
track = statement*
statement = token (<empty> token)*

<token>    = elem | assign | header | play
<term>     = [<empty>] number | [<empty>] [<'('>] add-sub [<')'>] [<empty>]
<elem>     = [<empty>] atom | prim | term | pair | list | set | identifier [<empty>]
<prim>     = [<empty>] string | number | meter | color [<empty>]
atom       = [<empty>] keyword [<empty>] init [<empty>]
set        = [<empty>] <'{'> [elem (<','|empty> elem)* [<','>]] <'}'> [<empty>]
list       = [<empty>] <'['> [elem (<','|empty> elem)* [<','>]] <']'> [<empty>]
pair       = term <'->'> elem [<empty>,<empty>]
assign     = identifier <'='> elem
header     = meta <'='> elem
attribute  = word [<empty>] <':'> [<empty>] prim
identifier = [<empty>] #':[a-zA-Z]+' [<empty>]
arguments  = ((identifier | string | attribute | add-sub) [<empty> <','> <empty>])*
init       = <'('> arguments <')'>
meta       = [<empty>] <'@'> #'(Audio|Instrument|Time|Tempo|Key|Delay|Link|Title|Artist|Desc|Tags)' [<empty>]
keyword    = [<empty>] #'(`|~|Note|Scale|Chord|Mode|Triad)' [<empty>]
play       = [<empty>] <'!Play'> [<empty>] elem
meter      = [<empty>] number <'|'> number [<empty>]
string     = #'[\'|\"](.*?)[\"|\']'
word       = #'[a-zA-Z]+'
number     = #'[0-9]+'
color      = #'#[a-fA-F0-9xX]{6}'
<empty>    = #'(\r\n|\n|\r|\s)*'

(* Math *)
<add-sub>  = mul-div | add | sub
add        = term [<empty>] <'+'> [<empty>] term
sub        = term [<empty>] <'-'> [<empty>] term
<mul-div>  = term | mul | div
mul        = term [<empty>] <'*'> [<empty>] term
div        = term [<empty>] <'/'> [<empty>] term
