(* Core *)
track = statement*
statement = token (<empty> token)*

<token>    = elem | assign | header | play
<term>     = [<empty>] number | [<'('>] add-sub [<')'>] [<empty>]
<elem>     = [<empty>] atom | prim | term | pair | list | identifier [<empty>]
<prim>     = [<empty>] string | number | color [<empty>]
atom       = [<empty>] keyword [<empty>] init [<empty>]
set        = [<empty>] <'{'> [elem (<','|empty> elem)* [<','>]] <'}'> [<empty>]
list       = [<empty>] <'['> [elem (<','|empty> elem)* [<','>]] <']'> [<empty>]
pair       = term <'->'> elem [<empty>,<empty>]
assign     = identifier <'='> elem
header     = meta <'='> elem
identifier = [<empty>] #':[a-zA-Z]+' [<empty>]
arguments  = ((identifier | string | color | add-sub) [<empty> <','> <empty>])*
init       = <'('> arguments <')'>
meta       = [<empty>] <'@'> #'(Instrument|Title|Time|Tempo|Tags|Key|Link)' [<empty>]
keyword    = [<empty>] #'(`|~|Note|Scale|Chord)' [<empty>]
play       = [<empty>] <'!Play'> [<empty>] elem
string     = #'[\'|\"](.*?)[\"|\']'
word       = #'[a-zA-Z]+'
number     = #'[0-9]+'
color      = #'#[a-fA-F0-9xX]{6}'
<empty>    = #'(\r\n|\n|\r|\s)*'

(* Math *)
<add-sub>  = mul-div | add | sub
add        = [<empty>] add-sub [<empty>] <'+'> [<empty>] mul-div [<empty>]
sub        = [<empty>] add-sub [<empty>] <'-'> [<empty>] mul-div [<empty>]
<mul-div>  = term | mul | div
mul        = mul-div <'*'> term
div        = mul-div <'/'> term
