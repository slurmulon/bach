(* Core *)
track = statement*
statement = token (<empty> token)*

<token>    = elem | assign
<term>     = [<empty>] number | [<'('>] add-sub [<')'>] [<empty>]
<atom>     = [<empty>] keyword [<empty>] init [<empty>]
<elem>     = [<empty>] atom | term | pair | color | identifier | list [<empty>]
list       = [<empty>] <'['>(elem [<','> elem])* <']'> [<empty>]
pair       = term <'->'> elem (*[<empty>,<empty>]*)
assign     = identifier <'='> (elem)
identifier = [<empty>] #':[a-zA-Z]+' [<empty>]
arguments  = ((identifier | string | color | add-sub) [<empty> <','> <empty>])*
init       = <'('> arguments <')'>
keyword    = [<empty>] #'(Note|Scale|Chord|Rest|Times|TimeSig|Tempo|Play|Stop|Forever)' [<empty>]
string     = #'[\'|\"](.*?)[\"|\']'
word       = letter+
number     = digit+
color      = #'#[a-fA-F0-9xX]{6}'
<letter>   = #'[a-zA-Z]'
<digit>    = #'[0-9]'
<empty>    = #'(\r\n|\n|\r|\s)*'

(* Math *)
<add-sub>  = mul-div | add | sub
add        = [<empty>] add-sub [<empty>] <'+'> [<empty>] mul-div [<empty>]
sub        = [<empty>] add-sub [<empty>] <'-'> [<empty>] mul-div [<empty>]
<mul-div>  = term | mul | div
mul        = mul-div <'*'> term
div        = mul-div <'/'> term
